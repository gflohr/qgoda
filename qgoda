#! /usr/bin/env perl

# Copyright (C) 2016 Guido Flohr <guido.flohr@cantanea.com>, 
# all rights reserved.

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use strict;

sub usage_error;
sub display_usage;
sub display_version;

use Getopt::Long;

use Qgoda;
use Locale::TextDomain qw(com.cantanea.qgoda);

my %options;
Getopt::Long::Configure('bundling');
GetOptions (
            'q|quiet' => \$options{quiet},
            'h|help' => \$options{help},
            'v|verbose' => \$options{verbose},
            'V|version' => \$options{version}
	    ) or exit 1;

display_usage if $options{help};
display_version if $options{version};

autoflush STDOUT, 1;
autoflush STDERR, 1;

usage_error __"Too many commands." if @ARGV > 1;

my $command = shift @ARGV;
$command ||= '';

my $method;
if ('config' eq $command) {
    delete $options{verbose};
    $options{quiet} = 1;
    $method = 'dumpConfig';
} elsif ('build' eq $command) {
	$method = 'build';
} elsif ('watch' eq $command) {
	$method = 'watch';
} else {
	usage_error __"Nothing to do.";
}

Qgoda->new(%options)->$method;

sub display_usage {
    my $msg = __x(q(Usage: {program} COMMAND [OPTIONS]
Mandatory arguments to long options, are mandatory to short options, too.

The following commands are currently supported:

  build                       build site and exit
  watch                       build, then watch for changes and build on demand
  config                      dump the current configuration and exit

Operation mode:
  -q, --quiet                 quiet mode
  -v, --verbose               display progress on standard error

Informative output:
  -h, --help                  display this help and exit
  -V, --version               output version information and exit

The Qgoda static site generator renders your site by default into the
directory "_site" inside the current working directory.  See
http://www.qgoda.net/ for more information.
), program => $0);

    print $msg;

    exit 0;
}

sub usage_error {
    my $message = shift;
    if ($message) {
        $message =~ s/\s+$//;
        $message = "$0: $message\n";
    }
    else {
        $message = '';
    }
    die <<EOF;
${message}Usage: $0 COMMAND [OPTIONS]
Try '$0 --help' for more information!
EOF
}

sub display_version {
    my $msg = __x('{program} (Qgoda static site generator) {version}
Copyright (C) {years} Cantanea EOOD (http://www.cantanea.com/).
License GPLv3+: GNU GPL version 3 or later <http://gnu.org/licenses/gpl.html>
This is free software: you are free to change and redistribute it.
There is NO WARRANTY, to the extent permitted by law.
Written by Guido Flohr (http://www.guido-flohr.net/).
', program => $0, years => 2016, version => $Qgoda::VERSION);

    print $msg;

    exit 0;
}

